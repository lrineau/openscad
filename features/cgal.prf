DEFINES += ENABLE_CGAL

QMAKE_CXXFLAGS += -std=c++14

# Optionally specify location of CGAL using the 
# CGALDIR env. variable
CGAL_DIR = $$(CGALDIR)
!isEmpty(CGAL_DIR) {
  QMAKE_INCDIR += $$CGAL_DIR/include
  QMAKE_LIBDIR += $$CGAL_DIR/lib
  message("CGAL location: $$CGAL_DIR")
}

exists($$CGAL_DIR/include/CGAL/compiler_config.h)|exists(/usr/include/CGAL/compiler_config.h) {
  LIBS += -lCGAL -lmpfr -lgmp
} else {
  # CGAL header-only
  DEFINES += CGAL_HEADER_ONLY
  LIBS += -lmpfr -lgmp
  message("Using CGAL header-only")
}

*g++* {
  QMAKE_CXXFLAGS += -frounding-math
}

*clang* {
  QMAKE_CXXFLAGS -= -frounding-math
}
